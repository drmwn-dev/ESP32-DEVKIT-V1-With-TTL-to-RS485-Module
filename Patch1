#include <ModbusMaster.h>

// ================= RS485 =================
#define RS485_RX 16
#define RS485_TX 17
#define RS485_DE 4
#define BAUDRATE 9600

HardwareSerial rs485(2);
ModbusMaster node;

// ============== SENSOR STRUCT ============
struct ModbusSensor {
  uint8_t  slaveID;
  uint8_t  function;     // 3=Holding, 4=Input
  uint16_t startAddress;
  uint8_t  quantity;
  uint8_t  dataType;     // 1=INT16, 3=FLOAT32 AB
  const char* name;
};

// ======== DAFTAR SENSOR (EDIT DI SINI) ===
ModbusSensor sensors[] = {
  {1, 3, 0,  2, 1, "XYMD02-TEMP-HUM"},
  {11, 3, 0,  2, 1, "PT100-TEMP-RESIST"},
  {3, 3, 10, 2, 3, "PH"},
  {4, 3, 20, 2, 3, "EC"}
};

const uint8_t TOTAL_SENSORS = sizeof(sensors) / sizeof(sensors[0]);
uint8_t sensorIndex = 0;

// ============== RS485 DIR =================
void preTransmission() {
  digitalWrite(RS485_DE, HIGH);
}
void postTransmission() {
  digitalWrite(RS485_DE, LOW);
}

// ============== PRINT =====================
void printSensor(ModbusSensor s) {
  Serial.print("[");
  Serial.print(s.name);
  Serial.print("] ");

  if (s.dataType == 1) { // INT16
    for (uint8_t i = 0; i < s.quantity; i++) {
      int16_t v = node.getResponseBuffer(i);
      Serial.print(v);
      Serial.print(" ");
    }
    Serial.println();
  }

  else if (s.dataType == 3 && s.quantity >= 2) { // FLOAT32 AB
    uint32_t raw = ((uint32_t)node.getResponseBuffer(0) << 16) |
                    node.getResponseBuffer(1);
    float f;
    memcpy(&f, &raw, 4);
    Serial.println(f);
  }
}

// ============== SETUP =====================
void setup() {
  Serial.begin(9600);

  pinMode(RS485_DE, OUTPUT);
  digitalWrite(RS485_DE, LOW);

  rs485.begin(BAUDRATE, SERIAL_8N1, RS485_RX, RS485_TX);

  node.preTransmission(preTransmission);
  node.postTransmission(postTransmission);

  Serial.println("ESP32 Multi-Slave Modbus READY");
}

// ============== LOOP ======================
void loop() {

  ModbusSensor s = sensors[sensorIndex];

  node.begin(s.slaveID, rs485);

  uint8_t result = 0xFF;
  if (s.function == 3) {
    result = node.readHoldingRegisters(s.startAddress, s.quantity);
  } else if (s.function == 4) {
    result = node.readInputRegisters(s.startAddress, s.quantity);
  }

  if (result == node.ku8MBSuccess) {
    printSensor(s);
  } else {
    Serial.print("[");
    Serial.print(s.name);
    Serial.print("] Error: ");
    Serial.println(result);
  }

  node.clearResponseBuffer();

  sensorIndex++;
  if (sensorIndex >= TOTAL_SENSORS) sensorIndex = 0;

  delay(1000);
}
